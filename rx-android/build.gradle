apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

apply from: "$rootDir/buildsystem/dependencies.gradle"

android {
    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 27
        versionCode stinson.versionCode
        versionName stinson.version
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile project(':core')
    compile project(':rx')

    implementation libraries.kotlin_stdlib
    implementation libraries.support_appcompat
    implementation libraries.rx
}

apply plugin: 'com.github.dcendents.android-maven'

group = stinson.group

install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'aar'
            groupId stinson.group
            artifactId stinson_rx_android.artifact

            name stinson.name
            description stinson.description
            url stinson.site

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }

            scm {
                connection stinson.github
                developerConnection stinson.github
                url stinson.site
            }
        }
    }
}

apply plugin: 'com.jfrog.bintray'

version = stinson.version

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

//task javadocJar(type: Jar, dependsOn: dokka) {
//    classifier = 'javadoc'
//    from dokka.outputDirectory
//}

artifacts {
//    archives javadocJar
    archives sourcesJar
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

    configurations = ['archives']

    pkg {
        repo = 'maven'
        name = stinson.name
        desc = stinson.description
        vcsUrl = stinson.github
        publish = true
        publicDownloadNumbers = true

        licenses = ['Apache-2.0']

        version {
            name = stinson.version
            desc = stinson_rx_android.versionDesc
            vcsTag = stinson.version
            released = new Date()
        }
    }
}
